buildscript {

    ext {
        springBootVersion = '2.2.4.RELEASE'
        // fix for not set system variable
        nexus_url = project.findProperty('nexus_url') ?: System.getenv('NEXUS_HOST')
        nexus_user = project.findProperty('nexus_user') ?: System.getenv('NEXUS_USERNAME')
        nexus_pw = project.findProperty('nexus_pw') ?: System.getenv('NEXUS_PASSWORD')
        no_nexus = (project.findProperty('no_nexus') ?: System.getenv('NO_NEXUS') ?: false).toBoolean()


    }

    repositories {
        if (no_nexus) {
            println("using repositories 'jcenter' and 'mavenCentral'")
            jcenter()
            mavenCentral()
        } else {
            println("using nexus repositories")
            if (!nexus_url) {
                throw new GradleException('Nexus URL not specified!')
            }
            maven() {
                url "${nexus_url}/repository/jcenter/"
                credentials() {
                    username = "${nexus_user}"
                    password = "${nexus_pw}"
                }
            }
            maven() {
                url "${nexus_url}/repository/maven-public/"
                credentials() {
                    username = "${nexus_user}"
                    password = "${nexus_pw}"
                }
            }
            maven() {
                url "${nexus_url}/repository/atlassian_public/"
                credentials {
                    username = "${nexus_user}"
                    password = "${nexus_pw}"
                }
            }
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8")
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version "2.8"
    id "com.diffplug.spotless" version "5.8.2"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'prov'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.11

repositories {
    if (no_nexus) {
        jcenter()
        mavenCentral()
    } else {
        maven() {
            url "${nexus_url}/repository/jcenter/"
            credentials() {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }
        maven() {
            url "${nexus_url}/repository/maven-public/"
            credentials() {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }
        maven() {
            url "${nexus_url}/repository/atlassian_public/"
            credentials {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-mail')
    implementation('org.springframework.boot:spring-boot-starter-security')
    //security frameowork
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    //templating for frontend
    implementation('org.springframework.boot:spring-boot-starter-web')
    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    //hot reloading, disabling cache ...

    annotationProcessor('org.projectlombok:lombok')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testImplementation('org.springframework.security:spring-security-test')

    implementation("org.thymeleaf.extras:thymeleaf-extras-springsecurity5")

    // oauth2
    implementation 'org.springframework.security:spring-security-oauth2-core'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.security:spring-security-jwt:1.1.0.RELEASE'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'

    implementation(group: 'com.openshift', name: 'openshift-restclient-java', version: '9.0.0.Final') {
        exclude(group: 'org.slf4j', module: 'slf4j-api:')
        exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    }

    // azure ad
    implementation('com.microsoft.azure:azure-active-directory-spring-boot-starter:2.2.2')

    //frontend webjars
    implementation 'org.webjars:webjars-locator:0.37'
    implementation 'org.webjars.bower:jquery:3.4.1'
    implementation 'org.webjars.bower:bootstrap:3.3.7'
    implementation 'org.webjars.bower:react:16.1.0'
    implementation 'org.webjars.bower:font-awesome:4.7.0'
    implementation 'org.webjars.bower:lodash:4.17.10'

    //encryption library for properties
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.1'

    //easy http calls to atlassian JSON APIs
    implementation "com.squareup.okhttp3:okhttp:4.4.0"

    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.10.0.pr3'
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.4'
    implementation(group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final')
    implementation(group: 'com.atlassian.security', name: 'atlassian-cookie-tools', version: '3.2.11')
    implementation(group: 'com.atlassian.crowd', name: 'crowd-integration-springsecurity', version: '1000.82.0') {
        exclude group: 'commons-httpclient'
        exclude group: 'org.apache.ws.commons', module: 'XmlSchema'
        // Explicity excludes vulnerable versions
        exclude group: 'org.apache.struts', module: 'struts2-core'
        exclude group: 'org.apache.struts.xwork', module: 'xwork-core'
        exclude group: 'commons-collections', module: 'commons-collections'
        exclude group: 'commons-fileupload', module: 'commons-fileupload'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.aspectj', module: 'aspectjweaver'
        exclude group: 'com.google.guava', module: 'guava'
    }
    // latest version of excluded libs: refactor this when upgrading to new 'com.atlassian.crowd:crowd-integration-springsecurity'
    implementation group: 'org.apache.struts', name: 'struts2-core', version: '2.5.20'
    implementation group: 'org.apache.struts.xwork', name: 'xwork-core', version: '2.3.37'
    implementation group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4'
    implementation group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.4'
    implementation group: 'com.google.guava', name: 'guava', version: '23.0'

}

bootJar {
    doFirst {
        copy {
            println 'bootJar: copy new frond end artifacts to resources folder!'
            from 'client/dist/client/nfe'
            into 'build/resources/main/static/nfe'
        }
    }
    launchScript()
    archiveFileName = "app.jar"
    destinationDirectory = file("$buildDir/../docker")
}

test {
    finalizedBy jacocoTestReport
}

bootRun {
    doFirst {
        copy {
            println 'bootRun: Copy New Front End files!!!'
            from 'client/dist/client/nfe'
            into 'build/resources/main/static/nfe'
        }
    }
    args = [
            '--spring.config.location=file:./build/resources/main/application-local.properties'
    ]
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
        indentWithSpaces(2)
    }
}

task yarnBuild(type:Exec) {

    workingDir 'client'

    //on windows:
    commandLine 'cmd', '/c', 'yarn', 'build'

    //on linux
    commandLine 'yarn', 'build'
}

bootRun.dependsOn yarnBuild
